/* Resolution of the linear elasticity system on a bridge */
load "medit"

/* Definition of the boundary curves of the domain */
border left(t=0.0,1.0){x=3.0*t ; y = 3.0-3.0*t ; label=0; };
border bot(t=0.0,6.0){x=3.0+t ; y = 0.0 ; label=0; };
border right(t=0.0,1.0){x=9.0+3.0*t ; y = 3.0*t ; label=0; };
border top(t=0,12.0){ x=12.0-t ; y=3.0; label=2;};
border left1(t=0.0,0.15){x=3.0*t ; y = 3.0-3.0*t; label=1; };
border bot1(t=0.45,1.55){x=t ; y = 2.55 ; label=0;  };
border l1(t=0.0,0.55){x=1.55-t ; y = 2.55-t; label=0; };
border p1(t=0.0,0.45){x=1.0+t ; y=2.0-t ; label=1;};
border r1(t=0.0,1.0){x=1.45+t ; y = 1.55+t ; label=0;};
border cc1(t=2.45,3.75) {x=t ; y = 2.55 ; label=0; };
border l4(t=0.0,2.55) {x=3.75 ; y=2.55-t ; label=0; };
border f4(t=0.0,0.5) {x=3.75+t ; y=0.0 ; label=1; };
border r4(t=0.0,2.55) {x=4.25 ; y=t ; label=0; };
border cc3(t=4.25,5.75) {x=t ; y = 2.55 ; label=0; };
border l3(t=0.0,2.55) {x=5.75 ; y=2.55-t ; label=0; };
border f3(t=0.0,0.5) {x=5.75+t ; y=0.0 ; label=1; };
border r3(t=0.0,2.55) {x=6.25 ; y=t ; label=0; };
border cc4(t=6.25,7.75) {x=t ; y = 2.55 ; label=0; };
border l5(t=0.0,2.55) {x=7.75 ; y=2.55-t ; label=0; };
border f5(t=0.0,0.5) {x=7.75+t ; y=0.0 ; label=1; };
border r5(t=0.0,2.55) {x=8.25 ; y=t ; label=0; };
border cc2(t=8.25,9.55) {x=t ; y = 2.55 ; label=0; };
border l2(t=0.0,1.0){x=9.55+t ; y = 2.55-t; label=0; };
border p2(t=0.0,0.45){x=10.55+t ; y = 1.55+t; label=1; };
border r2(t=0.0,0.55){x=11.0-t ; y = 2.0+t ; label=0; };
border bot2(t=10.45,11.55){x=t ; y = 2.55 ; label=0;  };
border right1(t=0.0,0.15){x=11.55+3.0*t ; y = 2.55+3.0*t ; label=1; };

/* Build mesh, display and save as a .mesh file */
mesh Th = buildmesh(left1(20)+bot1(20)+l1(20)+p1(20)+r1(20)+cc1(20)+l4(60)+f4(20)+r4(20)+cc3(20)+l3(60)+f3(20)+r3(20)+cc4(20)+l5(60)+f5(20)+r5(20)+cc2(40)+l2(20)+p2(20)+r2(20)+bot2(20)+right1(20)+top(200));
mesh Thn;
plot(Th,wait=1);
savemesh(Th,"br.mesh");

/* Definition of the Finite Element spaces and functions */
fespace Vh(Th,P1); // or P2
Vh ux,uy,vx,vy;

/* Other parameters */
real gx = 0.0;
real gy = -0.01;
real mu = 0.3846;
real lambda = 0.5769;

/* Linearized elasticity system */
problem elas([ux,uy],[vx,vy],solver=Crout) = int2d(Th)( 2.0*mu*(dx(ux)*dx(vx) + 0.5*(dy(ux)+dx(uy))*(dy(vx)+dx(vy)) + dy(uy)*dy(vy)) + lambda*(dx(ux)+dy(uy))*(dx(vx)+dy(vy)) )
          - int1d(Th,2)(gx*vx+gy*vy)
          + on(1,ux=0.0,uy=0.0);

/* Solve problem and display solution */
elas;
plot(Th,[ux,uy],fill=1);
savesol("br.sol",Th,[ux,uy]);

/* Move the mesh, display and save the displaced mesh */
Thn = movemesh(Th,[x+ux,y+uy]);
plot(Thn);
savemesh(Thn,"brn.mesh");