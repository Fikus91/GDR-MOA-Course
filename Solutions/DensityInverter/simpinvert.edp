/* Topology optimization of a gripping mechanism */
load "medit"

/* Create mesh */
border left1(t=0.0,0.1){ x=0.0; y=1.0-t; label=1; };
border left2(t=0.1,0.45){ x=0.0; y=1.0-t; label=0; };
border left3(t=0.45,0.55){ x=0.0; y=1.0-t; label=2; };
border left4(t=0.55,0.9){ x=0.0; y=1.0-t; label=0; };
border left5(t=0.9,1.0){ x=0.0; y=1.0-t; label=1; };
border bot(t=0.0,1.0){ x=t; y=0.0; label=0; };
border right1(t=0.0,0.45){ x=1.0; y=t; label=0; };
border right2(t=0.45,0.55){ x=1.0; y=t; label=3; };
border right3(t=0.55,1.0){ x=1.0; y=t; label=0; };
border top(t=0.0,1.0){ x=1.0-t; y=1.0; label=0; };

mesh Th = buildmesh(left1(10)+left2(35)+left3(10)+left4(35)+left5(10)+
                           bot(100)+right1(50)+right2(10)+right3(50)+top(100));

/* Finite Element spaces */
fespace Vh(Th,P1);
fespace Vh0(Th,P0);

/* Functions and other parameters */
Vh ux,uy,px,py,uxo,uyo,vx,vy,v,h,hdisp,htmp,ho,hr,hro,g;
Vh0 g0;
real gx = 100.0;
real gy = 0.0;
real mu = 0.3846;
real lambda = 0.5769;
real ers = 1.e-4;
real eps = 1.e-6;
real meshsiz = 0.02;
real alpha = 0.02;
real beta = 0.0;
real ks = 0.1;
real meas;
real ell = 50;
real vol,obj,newobj;
int maxit = 200, kmax = 10;
real step = 0.1;
real gmax;
real tol = 0.2;
real maxstep = 0.2;
real minstep = 0.002;
real ain = 1.0;
real aout = 2.0;
string sout;

/* Interpolation profile for the lamÃ© coefficients */
macro zetalm(t) ( lambda*(ers + (1.0-ers)*t^3) ) // EOM
macro zetalmprime(t) ( 3.0*lambda*(1.0-ers)*t^2 ) // EOM
macro zetamu(t) ( mu*(ers + (1.0-ers)*t^3) ) // EOM
macro zetamuprime(t) ( 3.0*mu*(1.0-ers)*t^2 ) // EOM

/* Approximate Heaviside function */
macro heav(t) ( 1.0-exp(-beta*t) + exp(-beta)*t ) // EOM
macro heavprime(t) ( exp(-beta) + beta*exp(-beta*t) ) // EOM

/* Regularization of density */
problem hregul(hr,v) = int2d(Th)(alpha^2*(dx(hr)*dx(v)+dy(hr)*dy(v)) + hr*v)
                      - int2d(Th)(h*v);

/* Regularization of shape derivative */
problem sdreg(g,v) = int2d(Th)(alpha^2*(dx(g)*dx(v)+dy(g)*dy(v)) + g*v)
                      - int2d(Th)(g0*v) 
                      + on(3,g=0);

/* Objective function */
macro J() (ain*int1d(Th,2)(ux)+aout*int1d(Th,3)(ux)) // EOM

/* Volume function */
macro volume(hr) (int2d(Th)(heav(hr))) // EOM

/* Frechet derivative */
macro dJ() {
  adjoint;
  g0 = zetamuprime(heav(hr))*heavprime(hr)*(2.0*dx(ux)*dx(px) + (dx(uy)+dy(ux))*(dx(py)+dy(px)) + 2.0*dy(uy)*dy(py))
       + zetalmprime(heav(hr))*heavprime(hr)*(dx(ux)+dy(uy))*(dx(px)+dy(py))  + ell*heavprime(hr);
  sdreg;
} // EOM

/* Truncation of the density field between 0 and 1 */
macro proj() {
  real r = 0.05; 
  htmp = h;
  func real truncsymh() {
    if ( y > 0.5 ) {
      if ( (x-1.0)^2 + (y-0.5)^2 < r^2 ) return(1.0); 
      else return (htmp(x,y));
    }
    else {
      if ( (x-1.0)^2 + (y-0.5)^2 < r^2 ) return(1.0);
      else return (htmp(x,1.0-y));
    }
  }
  h = truncsymh();
  h = min(1.0,max(h,0.0));
} // EOM

/* State problem */
problem elasticity([ux,uy],[vx,vy]) = int2d(Th)(zetamu(heav(hr))*(2.0*dx(ux)*dx(vx) + (dx(uy)+dy(ux))*(dx(vy)+dy(vx))
                           + 2.0*dy(uy)*dy(vy)) + zetalm(heav(hr))*(dx(ux)+dy(uy))*(dx(vx)+dy(vy)) )
                           + int1d(Th,3)(ks*(ux*vx+uy*vy))
                           - int1d(Th,2)(gx*vx+gy*vy)
                           + on(1,ux=0.0,uy=0.0);

/* Adjoint problem */
problem adjoint([px,py],[vx,vy]) = int2d(Th)(zetamu(heav(hr))*(2.0*dx(px)*dx(vx) + (dx(py)+dy(px))*(dx(vy)+dy(vx))
                                + 2.0*dy(py)*dy(vy)) + zetalm(heav(hr))*(dx(px)+dy(py))*(dx(vx)+dy(vy)) )
                                + int1d(Th,3)(ks*(px*vx+py*vy))
                                - int1d(Th,2)(-ain*vx) -int1d(Th,3)(-aout*vx)
                                + on(1,px=0.0,py=0.0);

/* Initialization: design full of high conductivity */
h = 0.5;
proj(h);
hregul;
elasticity;

/* Initial value of the objective function */
vol = volume(hr);
newobj = J + ell*vol;

cout<<"Initial value of the objective function: "<<newobj<<endl;

/* Main loop */
for (int it=0; it<maxit; it++) {
  
  /* Update coefficient of the heaviside projection */
  if ( it >= 75 && it % 10 == 0 && beta <=3 ) {
    beta = beta + 1;
    elasticity;
    vol = volume(hr);
    newobj = J + ell*vol;
  }
  
  obj = newobj;
  ho = h;
  hro = hr;
  uxo = ux;
  uyo = uy;
  
  /* Calculation of the shape gradient */
  dJ;
  gmax = max(g[].max,-g[].min);

  /* Line search */
  for (int k=0; k<kmax; k++) {
    /* Update of topology */
    h = h - step/(eps+gmax)*g;
    proj(h);

    /* Calculation of the new objective function */
    hregul;
    elasticity;
    vol = volume(hr);
    newobj = J + ell*vol;
  
    /* Decision */
    /* Accept iteration */
    if ( newobj < obj + tol*abs(obj) || step <= minstep ) {
      step = min(1.1*step,maxstep);
      break;
    }
    /* Reject iteration */
    else {
      step = max(0.6*step,minstep);
      h = ho;
      hr = hro;
      ux = uxo;
      uy = uyo;
      newobj = obj;
    }
  }
  
  /* Plot shape and print new objective */
  hdisp = heav(hr);
  plot(Th,hdisp,fill=1);
  sout = "box."+it+".sol";
  savesol(sout,Th,hdisp);
  savesol("box.sol",Th,hdisp);
  sout = "disp."+it+".sol";
  savesol(sout,Th,[ux,uy]);
  savesol("disp.sol",Th,[ux,uy]);
  cout<<"Iteration "<<it<<" Objective : "<<newobj<<endl;
}

